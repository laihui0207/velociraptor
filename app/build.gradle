buildscript {
    repositories {
        jcenter()
        google()
        maven { url 'https://maven.fabric.io/public' }
    }

    dependencies {
        classpath 'io.fabric.tools:gradle:1.25.1'
        classpath 'com.squareup.sqldelight:gradle-plugin:0.6.1'
        classpath 'com.google.gms:oss-licenses:0.9.1'
    }
}
apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'io.fabric'
apply plugin: 'com.squareup.sqldelight'
apply plugin: 'com.google.gms.oss.licenses.plugin'

android {
    compileSdkVersion 27
    buildToolsVersion "27.0.3"

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS_VELOCIRAPTOR
                keyPassword RELEASE_KEY_PASSWORD_VELOCIRAPTOR
            }
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE-FIREBASE.txt'
        exclude 'META-INF/NOTICE'
    }

    defaultConfig {
        applicationId "com.pluscubed.velociraptor"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 42
        versionName "1.6.5"
        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [appName: "@string/app_name"]
        multiDexEnabled true

        lintOptions {
            disable 'MissingTranslation'
            disable 'ExtraTranslation'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debugIap {
            debuggable true
            minifyEnabled false
            shrinkResources false

            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            } else {
                signingConfig signingConfigs.debug
            }
        }

        debug {
            minifyEnabled false
            useProguard false
            applicationIdSuffix '.debug'
            versionNameSuffix '-DEBUG'
            testCoverageEnabled false
            manifestPlaceholders = [appName: "Velociraptor DEBUG"]
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    jcenter()
    google()
    maven { url 'https://maven.fabric.io/public' }
    maven { url "https://jitpack.io" }
}

dependencies {
    testCompile 'junit:junit:4.12'
    testCompile "org.mockito:mockito-all:1.10.19"
    testCompile "org.hamcrest:hamcrest-all:1.3"
    testCompile "org.robolectric:robolectric:3.6.1"

    debugCompile 'im.dino:dbinspector:3.4.1@aar'
    debugIapCompile 'im.dino:dbinspector:3.4.1@aar'

    compile "com.google.auto.value:auto-value:1.4"
    annotationProcessor "com.google.auto.value:auto-value:1.4"
    annotationProcessor 'com.ryanharter.auto.value:auto-value-parcel:0.2.5'

    compile 'com.squareup.sqlbrite:sqlbrite:1.1.2'

    compile 'com.android.support:appcompat-v7:27.0.2'
    compile 'com.android.support:support-v4:27.0.2'
    compile 'com.android.support:support-v13:27.0.2'
    compile 'com.android.support:support-annotations:27.0.2'
    compile 'com.android.support:cardview-v7:27.0.2'
    compile 'com.android.support:design:27.0.2'
    compile 'com.android.support:customtabs:27.0.2'

    compile 'com.google.firebase:firebase-core:11.8.0'
    compile 'com.google.firebase:firebase-messaging:11.8.0'
    compile 'com.google.firebase:firebase-config:11.8.0'

    compile 'com.android.billingclient:billing:1.0'

    compile 'com.google.android.gms:play-services-location:11.8.0'
    compile 'com.google.android.gms:play-services-oss-licenses:11.8.0'
    compile 'com.google.android.gms:play-services-maps:11.8.0'
    compile 'com.google.maps.android:android-maps-utils:0.5'

    compile 'com.stepstone.stepper:material-stepper:4.3.1'

    compile 'com.afollestad.material-dialogs:core:0.9.6.0'

    compile 'io.reactivex:rxandroid:1.2.1'
    compile 'io.reactivex:rxjava:1.3.4'

    compile 'com.squareup.retrofit2:retrofit:2.3.0'
    compile 'com.squareup.retrofit2:adapter-rxjava:2.3.0'
    compile 'com.squareup.retrofit2:converter-jackson:2.3.0'
    compile 'com.squareup.retrofit2:converter-scalars:2.3.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.9.1'
    compile 'com.squareup.okhttp3:okhttp:3.9.1'
    compile 'com.squareup.okio:okio:1.13.0'

    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'
    compile 'com.fasterxml.jackson.core:jackson-core:2.9.4'
    compile 'com.fasterxml.jackson.core:jackson-annotations:2.9.4'

    compile 'com.github.gigamole.arcprogressstackview:library:1.0.3'
    compile 'com.github.pluscubed:recycler-fast-scroll:0.3.2'

    compile 'com.github.bumptech.glide:glide:3.7.0'

    compile 'com.jakewharton:butterknife:8.8.1'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.1'

    compile 'com.jakewharton.timber:timber:4.6.0'

    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.5.1'
    debugIapCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    testCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.1'

    compile('com.crashlytics.sdk.android:crashlytics:2.8.0@aar') {
        transitive = true;
    }
    compile 'com.android.support.constraint:constraint-layout:1.0.2'
    compile "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}


apply plugin: 'com.google.gms.google-services'